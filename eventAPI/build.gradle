plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.stackops'
version = '1.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
    mavenCentral()  // This usually includes the central repository
    maven { url 'https://repo.spring.io/release' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.postgresql:postgresql'
	
	testImplementation 'io.cucumber:cucumber-java:7.11.2'
    testImplementation 'io.cucumber:cucumber-junit:7.11.2'
    testImplementation 'io.cucumber:cucumber-spring:7.11.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured:5.3.0'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

tasks.register('unitTest', Test) {
    useJUnitPlatform()
    description = "Runs the unit tests."
    group = "verification"
    
    // Customize the include pattern for your unit tests if needed
    include '**/*ApplicationTest.class'
}

tasks.register('apiTest', Test) {
    useJUnitPlatform()
    description = "Runs the API (Cucumber) tests."
    group = "verification"

    // Ensure this includes only the Cucumber runner class
    include '**/RunCucumberTest.class'
}

tasks.named("test") {
    dependsOn tasks.named("unitTest")
    dependsOn tasks.named("apiTest")
}

tasks.withType(Test).configureEach {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true  // Show standard output and error during the tests
    }
}

